group = "com.prayag.omr_scanner_flutter_android"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "1.9.24"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.2.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"


def getGithubToken() {
    def propsFile = rootProject.file("secrets.properties")
    def props = new Properties()
    props.load(new FileInputStream(propsFile))
    def token = props.getProperty("GITHUB_TOKEN")
    if (token == null) {
        throw new GradleException("GITHUB_TOKEN not found in secrets.properties")
    }
    return token
}

def githubToken = getGithubToken()

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/Prayag887/omr_scanner")
        credentials {
            username = "Prayag887"
            password = githubToken
        }
    }
}



android {
    namespace = "com.prayag.omr_scanner_flutter_android"

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 24
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")

        //0.1.3 has lower threshold for bubbles and the main code comprises 0.1.3 as well
        //0.2.3 has additional feature for filtering out the question numbers if detected but its still in testing phase
        //0.3.5 is most accurate so far (Jul 24, 2025)
        implementation("com.prayag:omr-scanner:0.3.6") //0.3.6 has roll number feature added to it
    }

    packagingOptions {
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
